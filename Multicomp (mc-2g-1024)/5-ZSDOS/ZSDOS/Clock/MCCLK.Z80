	TITLE	"BIOS Clock Interface"
	SUBTTL	"Description of Clock Module"
;===================================================================
; MCCLK.Z80
; BIOS Clock driver for MULTICOMP variant 
;		Rienk Koolstra
; Version: 1 Oct 2016
;===================================================================

VERS	EQU	01
	.Z80
	NAME	('MCBIOS')

	MACLIB	CLOCK.LIB

	COMMON  /_CLKID/

DESCST:	DEFW	0		; Pointer to static year value if required

CLKNAM:	DEFB	'MULTICOMP BIOS Clock    '	; Exactly 24 chars in name
	DEFB    VERS/10+'0','.',VERS MOD 10 +'0',0

DESCR:	DEFB	'MULTICOMP BIOS Clock',0

	  IF  ($-DESCST) GT 256
	OVER2
	  ENDIF

	PAGE
	SUBTTL	"Configurable Clock Hardware Parameters"

	COMMON  /_PARM_/

PARBAS:	DEFW	0	; # of parameters (Set to 00 if none)
	DEFW    0	; Pointer to STRS (Set to 00 if none)

	CSEG

;-----------------------------------------------------------
;	Z S D O S  C L O C K  H E A D E R
;-----------------------------------------------------------
; Enter: HL points to a 6-byte buffer to Get/Set time
; Exit : A=1 on Success, A=FFH if error
;	HL points to last char in buffer
;	E contains original seconds (HL+5)

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;	R e a d / W r i t e  t h e  C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;CLKADD	EQU	0F216H
;MHZ	EQU	25

PRGBAS:	JP	GETTIM	; Jump to Read Clock
	JP      WRCLK	; Jump to Set Clock
;
GETTIM:	EX	DE,HL		; destination to DE
	LD	HL,(0FFFCH)	; Vector to time string in BIOS
	LD	BC,5		; Copy yymmddhhmm from BIOS
	LDIR			; 
	LD	A,(DE)		; original seconds value
	LDI			; read seconds
	EX	DE,HL		; Setup HL to point to seconds for return
	DEC	HL		; Need to dec HL back to seconds adr
	LD	D,0
	LD	E,A		; Original seconds value to E
	LD	A,1		; Signal success
	RET			; Done
;
WRCLK:	LD	DE,(0FFFCH)	; Vector to time string in BIOS
	PUSH	DE		; save for later
	LD	BC,6		; 6 bytes yymmddhhmmss
	LDIR			; copy to BIOS
	XOR 	A		; zero in tick count
	LD	(DE),A
	POP	DE		; retrieve time string
	DEC 	DE		; one before is MFlag
	LD	(DE),A		; signal new time set to BIOS
	INC	A		; indicate success
	RET			; Done
;

	PAGE
	SUBTTL	"Run-Time Configuration of Ports and Masks"

; This code installs configurable items into the clock module
; Enter with DE pointing to the physical address of the relocatable module

	COMMON  /_POST_/
	RET		; This RETURN MUST be present even if no other
			; code is included in this section
;

;---------------------------------------------------------------
; Read clock and wait for seconds to roll - watchdog protected
; Enter with: DE pointing to relocated clock read routine
;	HL pointing to base of high module

; This module is executed just prior to installing the module to insure
; that a valid clock is present
; Enter with DE pointing to beginning of relocated clock CSEG

	COMMON	/_PRE_/

	INCLUDE	PRECLOCK.LIB

	END
